;; Sri Ganeshji : Sri Balaji : Sri Pitreshwarji : Sri Durgaji : Sri Venkateshwara

/*

Description -
Query Box action is used to display the query box with two options. Now, a definition QueryBox
is introduced to provide multiple options.

Query Box Definition Syntax - 

[QueryBox : <Query Box Name>]
	Title 				: <Query box title>
	Horizontal Align 	: <Alignment of Query Box>
	Vertical Align 		: <Alignment of Query Box>
	Query 	: <Hot Key> : <Query option string> : <Additional description>
	Query 	: <Hot Key> : <Query option string> : <Additional description>
				:
				:
	Query 	: <Hot Key> : <Query option string> : <Additional description>
	Default : <Enter key option index> : <Escape key option index>

Action Syntax -
	Query Box Ex : <Query Box Name>
	
Form Attribute Control Syntax - 
	Control Ex	: <Query Box Name>	: <Logical Expression>[	: <Variable Name>]

*/

[Function: TSPL Smp Query Box]
	
	Variable	: SelectedIndex		: Number
	
	000	: Query Box Ex	: Query Box 1
	010	: Set			: SelectedIndex			: $$LastResult
	020	: Msg Box		: "Selected Index is "	: ##SelectedIndex
	
[Query Box: Query Box 1]

	Title				: "Query Box"
	Horizontal Align	: Right
	Vertical Align		: Top
	
;; 1 is set as default selection for Enter Key and 3 is set as default for Escape Key
	Default				: 1 	: 3	
	Gray Background		: No
	
;; Multiple Query Options can be specified
	Query				: Y 	: "Accept" 		: "Accept the Screen"
	Query				: O		: "Override" 	: "Override the existing data"
	Query				: N		: "Back" 		: "Cancel the operation"

;; End-of-File