;; Sri Ganeshji : Sri Balaji : Sri Pitreshwarji : Sri Durgaji : Sri Venkateshwara

/*
Objective(s) – 
-	This code demonstrates the capability of gathering XML Collection to 
	retrieve the data from XML File present at a specified URL. Further, it 
	displays the data fetched in a Report.

Specific TDL feature(s) used –
-	External ASCII XML File at a predefined URL as Data Source for Collection

Last modification – 
-	Altered on 26/10/2009

Dependencies – 
-	NA
*/

[Report: DSA XML Get Collection]

	Form 	: DSA XML Get Collection
	Title 	: "Simple Get Request "

[Form: DSA XML Get Collection]

	Height	: 100% Screen
	Width	: 100% Screen
	Parts	: DSA XML Get Collection, XMLData
		
[Part: DSA XML Get Collection]

	Lines		: Note, DSA XML Get Collection 
	Repeat		: DSA XML Get Collection : DSA XML Get Collection	
	Scroll  	: Vertical
	Local: Line	: Note	: Local	: Field: Name Field	: Set As: "(ASCII XML file is being read as Data Source)"
	Local: Line	: Note	: Local	: Field: Name Field	: Lines	: 0
	Local: Line	: Note	: Local	: Field: Default	: Style	: Normal Italic
	Local: Line	: Note	: Space Bottom : 1

	[Line: DSA XML Get Collection]

		Fields	: DSA XML Name, DSA XMLEmp ID
		Explode : DSA XML Phone
		Explode : DSA XML Add
		
		[Field: DSA XMLName]

			Set As 	: "Name : " + $NAME
		
		[Field: DSA XMLEmp ID]

			Set As 	: "Employee ID :" +  $EMPID
			Indent 	: 10

;; Explode Part - XML Phone

[Part: DSA XML Phone]

	Lines 	: DSA XML Get CollPhone
	Repeat 	: DSA XML Get CollPhone		: Phone
	Scroll	: Vertical

	[Line: DSA XML Get CollPhone]

		Fields	: Name Field, DSA XML Office, DSA XML Home, DSA XML Mobile
		Local 	: Field	: Name Field	: Set As 	: "Phone No"
			
		[Field: DSA XML Office]

			Set As 	: "Office:" + $OfficeNo
		
		[Field: DSA XML Home]

			Set As 	: "Home :" + $HomeNo
		
		[Field: DSA XML Mobile]

			Set As 	: "Mobile :" + $Mobile				
			
;; Explode Part - XML Add

[Part: DSA XML Add]

	Lines	: DSA XML Get CollAdd
	Repeat 	: DSA XML Get CollAdd 	: Address
	Scroll	: Vertical

	[Line: DSA XML Get CollAdd]

		Fields	: DSA XML Add1
		Explode : DSA XML Addr Line

		[Field: DSA XML Add1]

			Use 	: Name Field
			Set As 	: "Address"

;; Explode Part - XML Addr Line

[Part: DSA XML Addr Line]

	Lines	: DSA XML Get Addrline
	Repeat 	: DSA XML Get Addrline 	: AddrLine
	Scroll	: Vertical

	[Line: DSA XML Get Addrline]

		Fields	: DSA XML Add2

		[Field: DSA XML Add2]

			Set As 	: $ADDRLINE
			Indent 	: 5

;;Collection retrieving data from an XML file located at a predefined URL 

[Collection: DSA XML Get Collection]

	Data Source : HTTP XML   : "http://localhost/TestXMLASCII.XML" : ASCII ;;URL specification
	Client Only	: Yes

;; End-of-File
