;; Sri Ganeshji : Sri Balaji : Sri Pitreshwarji : Sri Durgaji : Sri Venkateshwara

/*
Objective(s) – 
-	This program demonstrates the capability of XML Collection to post request data from
	Tally to a Predefined URL and retrieve the response XML in Collection
-	The XML response thus fetched from the XML File is displayed in a Report.

Syntax -
-	Collection Attribute 'Remote URL' Syntax
	Remote URL 		: <URL Specification>
-	Remote Request Report
	Remote Request	: <Post-Request-Report-Name>, <Request-Report-Name> : <Encoding>

Last modification – 
-	Altered on 07/11/2009

Dependencies -
-	Supporting files 'XML Data.txt', 'XML Request to Server.txt' & 'Simple Get.txt'
-	PHP File 'CXMLResponse.php' which must be available at C:\INETPUB\WWWROOT in
	case if IIS is setup as the Web Server
*/

;; Report Displaying XML response fetched 

[Report: XML Post Collection]

	Form 	: XML Post Collection
	Title 	: "Simple Post Request"

[Form: XML Post Collection]

	Parts		: XML Post Collection
	Bottom Parts: XML Code
	Height		: 100% Screen
	Width		: 100% Screen

[Part: XML Post Collection]

	Lines	: XML Post Collection
	Repeat	: XML Post Collection	: XML Post Collection	
	Scroll  : Vertical
	Width 	: 100% Screen

	[Line: XML Post Collection]

		Fields	: XML Name, XMLEmp ID
		Explode : XML Phone
		Explode : XML Add

[Part: XML Code]

	Parts 	: XML Request, XML Data 
	Width 	: 100% Screen
	Local 	: Line	: XML Data		: Local	: Field : XML Data		: Set As : "XML Response from the Remote Server"
	Local 	: Line	: XML Request3	: Local	: Field	: XML Request 	: Set As : "<NAME>Tally</NAME>"
	Local 	: Line	: XML Request4	: Local	: Field	: XML Request 	: Set As : "<EMPID>00000</EMPID>"
	Local 	: Line	: XML Data2		: Local	: Field	: XML Data		: Set As : "<NAME>Tally</NAME>"
	Local 	: Line	: XML Data3		: Local	: Field	: XML Data		: Set As : "<EMPID>00000</EMPID>"


;;Collection retrieving XML response from a predefined URL 

[Collection: XML Post Collection]

	Remote URL		: "http://Localhost/CXMLResponse.php" ;;URL Specification
	RemoteRequest  	: XMLPostReqRep : ASCII               ;;Post Request Specification
	Client Only		: Yes

;;Report for Post Request

[Report: XMLPostReqRep]

	Form  	: XMLPostReqRep
	
[Form: XMLPostReqRep]

    Parts 	: XMLPostReqRep
 
[Part: XMLPostReqRep]

    Lines		: XMLPostReqRep
	Scroll		: Vertical
	XML Tag 	: "REQUEST"

	[Line: XMLPostReqRep]

		Fields 		: XMLPostReqRepName, XMLPostReqRepPwd

      	[Field: XMLPostReqRepName]

        	Set As 	 	: "Tally"
			XML Tag	 	: "NAME"

		[Field: XMLPostReqRepPwd]

   		    Set As   	: "00000"
          	XML Tag 	: "EMPID"

;; End-of-File
