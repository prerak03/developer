;; Sri Ganeshji : Sri Balaji : Sri Pitreshwarji : Sri Durgaji : Sri Venkateshwara

/*
Objective(s) – 
- 	This code demonstrates the Enhancement done on Data Source Attribute
	within Collection - Report

Last modification – 
-	Altered on 09/11/2009
*/

;; Current Report as Data Source

[Collection: Data Source Report Selected]

	Data Source : Report 		: Selected Lines
	Client Only : Yes

[Button: Data Source Report Selected]

	Key 	: Ctrl + S
	Action  : CALL	: Data Source Report Selected
	Title 	: "Log Selected"

;; Original Display Report

[Report: Data Source Report Selected]
	
	Form 		: Data Source Report Selected
	FullScreen	: No

[Form: Data Source Report Selected]

	Parts 	: Data Source Report Selected
	Button	: Data Source Report Selected

[Part : Data Source Report Selected]
	
	Lines 	: Data Source Report Selected
	Scroll 	: Vertical
	Repeat	: Data Source Report Selected : Group
	
	[Line: Data Source Report Selected]

		Fields	: Data Source Report Selected
		
		[Field: Data Source Report Selected]
	
			Set as : $Name

;; Function displaying the log using current report as Data Source

[Function: Data Source Report Selected]

	00	: SET FILE LOG ON
	10	: LOG	: "-----------------------------------------"
	20	: WALK COLLECTION	: Data Source Report Selected
	30	: 	LOG	: $Name		;; Selected Lines within the current Report will be logged
	40	: END WALK
	50	: SET FILE LOG OFF
	60	: EXEC COMMAND	: "TDLFunc.LOG"

;; End-of-File
